import org.gradle.internal.os.OperatingSystem

apply plugin: "google-test"

def prebuiltPath = "publish"

def libName(def binary, def name) {
    binary.targetPlatform.operatingSystem.windows ? "${name}.lib" : "lib${name}.a"
}

model {
    binaries {
        withType(TestSuiteBinarySpec) {
            lib project: ":utils:gtest", library: "gtest", linkage: "static"
            lib project: ":utils:gtest", library: "gmock", linkage: "static"

            if (toolChain in Gcc) {
                linker.args "-pthread"
            }

            // TODO:DAZ buildable is not being set correctly on test suite binaries
            if (it.testedBinary.buildable) {
                println "Adding dependency on $it"
                // TODO: This should be it.tasks.run, which should use the install image of the test exe
                unitTest.dependsOn it.namingScheme.getTaskName("run")
            }
        }
    }

    toolChains {
        visualCpp(VisualCpp)
        gcc(Gcc)
        mingw(Gcc) {
            path new File("C:/MinGW/bin")
            target("mingw")
        }
        if (OperatingSystem.current().isMacOsX()) {
            clang(Clang)
        }
    }

    platforms {
        win {
            operatingSystem "windows"
            architecture "x86_64"
        }
        mingw {
            operatingSystem "windows"
            architecture "x86_64"
        }
        osx {
            operatingSystem "osx"
            architecture "x86_64"
        }
        linux {
            operatingSystem "linux"
            architecture "x86_64"
        }
    }

    components {
        all {
            targetPlatform "win"
            targetPlatform "mingw"
            targetPlatform "osx"
            targetPlatform "linux"
        }
    }
}

task unitTest
